###############################################################################
#
# Description       : CMake build script for swigging the C# library
# Original author(s): Frank Bergmann <fbergman@caltech.edu>
# Organization      : California Institute of Technology
#
# This file is part of libSBML.  Please visit http://sbml.org for more
# information about SBML, and the latest version of libSBML.
#
# Copyright (C) 2013-2014 jointly by the following organizations:
#     1. California Institute of Technology, Pasadena, CA, USA
#     2. EMBL European Bioinformatics Institute (EMBL-EBI), Hinxton, UK
#     3. University of Heidelberg, Heidelberg, Germany
#
# Copyright (C) 2009-2013 jointly by the following organizations: 
#     1. California Institute of Technology, Pasadena, CA, USA
#     2. EMBL European Bioinformatics Institute (EMBL-EBI), Hinxton, UK
#  
# Copyright (C) 2006-2008 by the California Institute of Technology,
#     Pasadena, CA, USA 
#  
# Copyright (C) 2002-2005 jointly by the following organizations: 
#     1. California Institute of Technology, Pasadena, CA, USA
#     2. Japan Science and Technology Agency, Japan
# 
# This library is free software; you can redistribute it and/or modify it
# under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation.  A copy of the license agreement is provided
# in the file named "LICENSE.txt" included with this software distribution
# and also available online as http://sbml.org/software/libsbml/license.html
#
###############################################################################

message("Swig: C# Bindings")

file(TO_NATIVE_PATH ${CUR_BIN_DIRECTORY}/csharp-files LIBSBML_CS_FILES)

# delete previously generated swig files
if (EXISTS ${LIBSBML_CS_FILES})
 execute_process(
     COMMAND "${CMAKE_COMMAND}"
     -E remove_directory "${LIBSBML_CS_FILES}"
 )
endif()

# generate documentation if needed
if (WITH_DOXYGEN)
  # separate munged arguments
  separate_arguments(SWIG_SWIGDOCDEFINES)

  message("Creating csdoc.i")
  execute_process(
    COMMAND "${PYTHON_EXECUTABLE}"
            "${CUR_SRC_DIRECTORY}/../swig/swigdoc.py"
            --language csharp
            --top "${SRC_DIRECTORY}"
            --master "${CUR_SRC_DIRECTORY}/../swig/libsbml.i"
            --output "${CUR_BIN_DIRECTORY}/csdoc.i"
            ${SWIG_SWIGDOCDEFINES}
  )
else()
  file(WRITE ${CUR_BIN_DIRECTORY}/csdoc.i "/* documentation not generated */")
endif()

# create csharp-files directory
if (NOT EXISTS ${LIBSBML_CS_FILES})
  message("Creating ${LIBSBML_CS_FILES}")
  file(MAKE_DIRECTORY ${LIBSBML_CS_FILES})
endif()

# separate munged arguments
separate_arguments(SWIG_ARGS)

# execute swig
execute_process(
    COMMAND "${SWIG_EXECUTABLE}"
         -I${CUR_SRC_DIRECTORY}/../swig/
         -I${BIN_DIRECTORY}/src
         -I${SRC_DIRECTORY}/src
         -I${SRC_DIRECTORY}/include
         -I${CUR_SRC_DIRECTORY}
         -c++
         -csharp
         -namespace libsbmlcs
         -dllimport ${LIB_NAME}
         ${SWIG_ARGS} 
         -outdir ${LIBSBML_CS_FILES}
         -o ${CUR_BIN_DIRECTORY}/libsbml_wrap.cpp
         ${CUR_SRC_DIRECTORY}/../swig/libsbml.i
    WORKING_DIRECTORY "${CUR_BIN_DIRECTORY}"
)
